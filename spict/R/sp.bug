model{
# prior distribution of K: lognormal with 10% and 90% quantile at 80 and 300
K ~ dlnorm(5.042905,3.76)I(10,1000);
# prior distribution of r: lognormal with 10% and 90% quantile at 0.13 and 0.48
r ~ dlnorm(-1.38,3.845)I(0.01,1.2);
# prior distribution of q: instead of improper (prop. to 1/q) use just proper IG
iq ~ dgamma(0.001,0.0012)I(0.5,100);
q <- 1/iq;
# prior distribution of sigma2: inv. gamma with 10% and 90% qu. at 0.04 and 0.08
isigma2 ~ dgamma(3.785518,0.0102232);
sigma2 <- 1/isigma2;
# prior distribution of tau2: inv. gamma with 10% and 90% qu. at 0.05 and 0.15
itau2 ~ dgamma(1.709,0.00861342);
tau2 <- 1/itau2;
# (conditional) prior distribution of Ps (from state equations):
Pmed[1] <- 0;
P[1] ~ dlnorm(Pmed[1],isigma2) #I(0.001,2.0)
for (t in 2:N) { 
    Pmed[t] <- log(max(P[t-1] + r*P[t-1]*(1-P[t-1]) - C[t-1]/K, 0.001));
    P[t] ~ dlnorm(Pmed[t],isigma2) #I(0.001,2.0) 
}
# sampling distribution:
for (t in 1:N) { 
    Imed[t] <- log(q*K*P[t]);
    I[t] ~ dlnorm(Imed[t],itau2);
}
# further management parameters and predictions:
MSP <- r*K/4;
EMSP <- r/(2*q);
P1990 <- P[N]+r*P[N]*(1-P[N]) -C[N]/K;
B1990 <- P1990*K;
}